apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.novoda.bintray-release'


android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$versions.supportLibrary"
    implementation "com.squareup:seismic:$versions.seismic"

    androidTestImplementation "com.android.support.test:runner:$versions.testRunner"
    androidTestImplementation "com.android.support.test:rules:$versions.testRunner"
    androidTestImplementation "org.hamcrest:hamcrest-library:$versions.hamcrest"
    androidTestImplementation("org.mockito:mockito-core:$versions.mockitoCore") {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:$versions.dexmakerMokito"
    androidTestImplementation "com.nhaarman:mockito-kotlin:$versions.mockitoKotlin"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

def siteUrl = 'https://github.com/tomoima525/debot'

publish {
    userOrg = 'tomoima525'
    groupId  = 'com.tomoima.debot'
    artifactId = 'debot'
    bintrayUser = bintrayUserProperty
    bintrayKey = bintrayApiKeyProperty
    version =  library_version
    desc = 'A simple Android library to create Debugging menu.'
    website = siteUrl
    licences = ['Apache-2.0']
}

def getBintrayUserProperty() {
    return hasProperty('BINTRAY_USER') ? BINTRAY_USER : ""
}

def getBintrayApiKeyProperty() {
    return hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ""
}

// Avoid Kotlin docs error
tasks.withType(Javadoc) {
    enabled = false
}

repositories {
    mavenCentral()
}

apply from: rootProject.file('gradle/gradle-artifactory-upload.gradle')
